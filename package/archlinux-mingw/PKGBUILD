# $Id:
# Maintainer: Ivailo Monev <xakepa10@gmail.com>

pkgname=mingw-w64-katie-git
pkgver=4.9.0.aa9772c
pkgrel=1
pkgdesc='C++ toolkit derived from the Qt 4.8 framework (mingw-w64)'
arch=('i686' 'x86_64')
url='https://github.com/fluxer/katie'
license=('LGPL' 'FDL' 'custom')
# FIXME: depend on mingw-w64-vlc
depends=('mingw-w64-openssl' 'mingw-w64-zlib' 'mingw-w64-crt')
makedepends=('cmake' 'git' 'python' 'vlc' 'unifdef' 'mingw-w64-gcc')
optdepends=('vlc: Phonon VLC backend')
source=("git+https://github.com/fluxer/katie")
sha1sums=('SKIP')
conflicts=('mingw-w64-katie')
options=('!strip' 'staticlibs' '!buildflags') # 'debug')

_arch="${CARCH}-w64-mingw32"

pkgver() {
    cd katie
    printf "4.9.0.%s" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build

    echo "set(DBUS_INTERFACES_INSTALL_DIR /usr/${_arch}/etc/dbus-1/interfaces)" \
        >> katie/mkspecs/win32/vendor.cmake
    echo "set(CMAKE_INSTALL_DIR /usr/${_arch}/share/cmake)" \
        >> katie/mkspecs/win32/vendor.cmake
    echo 'set(QT_LIBRARIES_PATH "${QT_PREFIX_PATH}/lib/katie")' \
        >> katie/mkspecs/win32/vendor.cmake
    echo 'set(QT_PLUGINS_PATH "${QT_LIBRARIES_PATH}/plugins")' \
        >> katie/mkspecs/win32/vendor.cmake
    echo 'set(QT_IMPORTS_PATH "${QT_LIBRARIES_PATH}/imports")' \
        >> katie/mkspecs/win32/vendor.cmake
    echo 'set(QT_BINARIES_PATH "${QT_LIBRARIES_PATH}/bin")' \
        >> katie/mkspecs/win32/vendor.cmake
    echo 'set(QT_DATA_PATH "${QT_PREFIX_PATH}/share")' \
        >> katie/mkspecs/win32/vendor.cmake
}

build() {
    cd build
    cmake ../katie \
        -Wno-dev \
        -DCMAKE_TOOLCHAIN_FILE=cmake/archlinux-mingw.cmake \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/${_arch}
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install

    ${_arch}-strip --strip-unneeded "${pkgdir}/usr/${_arch}/lib/katie/"*.dll

    install -vDm644 ../katie/LGPL_EXCEPTION.txt \
        "${pkgdir}/usr/share/licenses/mingw-w64-katie-git/LGPL_EXCEPTION.txt"

    # bootstrap files, they should not be installed and this should be fixed
    # in Katie itself
    rm -rf "${pkgdir}/usr/local" "${pkgdir}/usr/share"
}
