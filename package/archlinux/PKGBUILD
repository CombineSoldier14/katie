# $Id:
# Maintainer: Ivailo Monev <xakepa10@gmail.com>

pkgname=katie-git
pkgver=4.9.0.8e624df
pkgrel=1
pkgdesc='C++ toolkit derived from the Qt 4.8 framework'
arch=('i686' 'x86_64')
url='https://github.com/fluxer/katie'
license=('LGPL' 'FDL' 'custom')
# nas is avaiable trough AUR, add it to depends array if desired
depends=('openssl' 'zlib' 'cups' 'alsa-lib' 'gtk2' 'libgl' 'libice'
        'libsm' 'libxcursor' 'libxext' 'libxfixes' 'libxi' 'libxinerama'
        'libxrandr' 'libxrender' 'libx11' 'fontconfig' 'freetype2' 'glib2')
makedepends=('cmake' 'git' 'python' 'postgresql' 'mariadb' 'unixodbc' 'mesa'
        'unifdef')
optdepends=('postgresql-libs: PostgreSQL driver'
        'libmariadbclient: MariaDB driver'
        'unixodbc: ODBC driver')
source=("git+https://github.com/fluxer/katie")
sha1sums=('SKIP')
conflicts=('katie')
# options=('debug')

pkgver() {
    cd katie
    printf "4.9.0.%s" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build

    echo 'set(LDCONF_INSTALL_DIR /etc/ld.so.conf.d)' \
        >> katie/mkspecs/linux/vendor.cmake
    echo 'set(PROFILE_INSTALL_DIR /etc/profile.d)' \
        >> katie/mkspecs/linux/vendor.cmake
    echo 'set(CMAKE_INSTALL_DIR /usr/share/cmake)' \
        >> katie/mkspecs/linux/vendor.cmake
}

build() {
    cd build
    cmake ../katie \
        -Wno-dev \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install

    install -vDm644 ../katie/LGPL_EXCEPTION.txt \
        "${pkgdir}/usr/share/licenses/katie-git/LGPL_EXCEPTION.txt"

    # register applications
    install -v -Dm644 ../katie/src/tools/designer/images/designer.png \
        "${pkgdir}/usr/share/pixmaps/designer-katie.png"
    install -v -Dm644 ../katie/src/tools/qdbusviewer/images/qdbusviewer.png \
        "${pkgdir}/usr/share/pixmaps/qdbusviewer-katie.png"
    mkdir -p "${pkgdir}/usr/share/applications"
    cat > "${pkgdir}/usr/share/applications/designer-katie.desktop" << EOF
[Desktop Entry]
Name=Katie Designer
Comment=Design GUIs for Katie applications
Exec=designer-katie
Icon=designer-katie.png
MimeType=application/x-designer;
Terminal=false
Encoding=UTF-8
Type=Application
Categories=Qt;Development;
EOF
    cat > "${pkgdir}/usr/share/applications/qdbusviewer-katie.desktop" << EOF
[Desktop Entry]
Name=Katie D-Bus Viewer
Comment=Debug D-Bus applications
Exec=qdbusviewer-katie
Icon=qdbusviewer-katie.png
Terminal=false
Encoding=UTF-8
Type=Application
Categories=Qt;Development;Debugger;
EOF
}
