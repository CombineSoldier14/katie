# $Id:
# Maintainer: Ivailo Monev <xakepa10@gmail.com>

pkgname=katie-git
pkgver=4.9.0.rc18.r16.gea38d0ce
pkgrel=1
pkgdesc='C++ toolkit derived from the Qt 4.8 framework'
arch=('i486' 'i686' 'pentium4' 'x86_64' 'arm')
url='https://github.com/fluxer/katie'
license=('LGPL' 'FDL' 'custom')
depends=('libsm' 'libxcursor' 'libxinerama' 'libmng' 'icu' 'libcups' 'libxrandr' 'sqlite' 'fontconfig')
makedepends=('cmake' 'git' 'postgresql' 'libmariadbclient' 'unixodbc' 'unifdef')
optdepends=('postgresql-libs: PostgreSQL driver'
        'libmariadbclient: MariaDB driver'
        'unixodbc: ODBC driver')
source=("git+https://github.com/fluxer/katie")
sha1sums=('SKIP')
conflicts=('katie')
# options=('debug')

pkgver() {
    cd katie
    #printf "4.9.0."r%s.%s "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../katie \
        -Wno-dev \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKATIE_SETTINGS_FULL=/etc/xdg \
        -DKATIE_LDCONF_FULL=/etc/ld.so.conf.d \
        -DKATIE_PROFILE_FULL=/etc/profile.d \
        -DKATIE_TOOLS_SUFFIX="-katie" \
        -DKATIE_ALLINONE=ON
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install

    install -vDm644 ../katie/LGPL_EXCEPTION.txt \
        "${pkgdir}/usr/share/licenses/katie-git/LGPL_EXCEPTION.txt"
}
