# $Id:
# Maintainer: Ivailo Monev <xakepa10@gmail.com>

pkgname=katie-git
pkgver=4.9.0.5ffb903a
pkgrel=1
pkgdesc='C++ toolkit derived from the Qt 4.8 framework'
arch=('i686' 'x86_64')
url='https://github.com/fluxer/katie'
license=('LGPL' 'FDL' 'custom')
depends=('openssl' 'zlib' 'cups' 'libice' 'icu' 'libsm' 'pcre' 'libxcursor'
        'libxext' 'libxfixes' 'libxi' 'libxinerama' 'libxrandr' 'libxrender'
        'libx11' 'fontconfig' 'freetype2' 'glib2' 'libmng')
makedepends=('cmake' 'git' 'python' 'postgresql' 'mariadb' 'unixodbc' 'unifdef')
optdepends=('postgresql-libs: PostgreSQL driver'
        'libmariadbclient: MariaDB driver'
        'unixodbc: ODBC driver')
source=("git+https://github.com/fluxer/katie")
sha1sums=('SKIP')
conflicts=('katie')
# options=('debug')

export CFLAGS="${CFLAGS} -flto" CXXFLAGS="${CXXFLAGS} -flto"

pkgver() {
    cd katie
    printf "4.9.0.%s" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../katie \
        -Wno-dev \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKATIE_LDCONF_FULL=/etc/ld.so.conf.d \
        -DKATIE_PROFILE_FULL=/etc/profile.d \
        -DKATIE_TOOLS_SUFFIX="-katie"
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install

    install -vDm644 ../katie/LGPL_EXCEPTION.txt \
        "${pkgdir}/usr/share/licenses/katie-git/LGPL_EXCEPTION.txt"
}
